plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'jacoco'
    id "io.freefair.lombok" version "6.1.0"
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group = 'software.crldev'
sourceCompatibility = '11'
setArchivesBaseName('elrond-spring-boot-starter-reactive')
setVersion('1.0.4')

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.logging.log4j') {
            details.useVersion '2.17.1'
        }
    }
}

dependencies {

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.6.2'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '2.6.2'

    implementation group: 'org.bouncycastle', name: 'bcmail-jdk15on', version: '1.69'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.69'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
    implementation group: 'org.bouncycastle', name: 'bcprov-ext-jdk15on', version: '1.69'
    implementation group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.15.10'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.6.2'
    testImplementation 'io.projectreactor:reactor-test:3.4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

def rootPackage = 'software.crldev.elrondspringbootstarterreactive'

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8

                excludes = [
                        rootPackage + '.error.*',
                        rootPackage + '.api.model.*',
                        rootPackage + '.util.MnemonicsUtils.KeyAndChainCode',
                        rootPackage + '.ErdNetworkAutoConfiguration',
                        rootPackage + '.TransactionSenderServiceAutoconfiguration',
                        rootPackage + '.ErdClientAutoConfiguration',
                        rootPackage + '.ErdInteractorAutoConfiguration',
                        rootPackage + '.interactor.WrappedResponses',
                        rootPackage + '.config.ErdClientConfig',
                        rootPackage + '.config.AddressConstants',
                        rootPackage + '.config.ErdNetworkConfigSupplier',
                ]
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestCoverageVerification)
}

jacocoTestReport {
    dependsOn test
}

task javadocJar(type: Jar) {
    classifier('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier('sources')
    from sourceSets.main.delombokTask
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name.set('Elrond Spring Boot Starter Reactive')
                description.set('A SpringBoot Starter solution designed to ensure easy and efficient integration with the Elrond Network using a Reactive API layer.')
                url.set('https://github.com/crldev-software/elrond-spring-boot-starter-reactive')
                licenses {
                    license {
                        name.set("MIT")
                        url.set("https://github.com/crldev-software/elrond-spring-boot-starter-reactive/blob/main/LICENSE")
                    }
                }
                developers {
                    developer {
                        id.set('carlo_stanciu')
                        name.set('Carlo Stanciu @ crldev.software')
                        email.set('carlo@crldev.software')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/crldev-software/elrond-spring-boot-starter-reactive.git')
                    developerConnection.set('scm:git:git://github.com/crldev-software/elrond-spring-boot-starter-reactive.git')
                    url.set('https://github.com/crldev-software/elrond-spring-boot-starter-reactive')
                }
            }
        }
    }
}

def decodeBase64 = { String value -> new String(Base64.getDecoder().decode(value)) }

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username.set(decodeBase64(System.getenv("OSSRH_USER")))
            password.set(decodeBase64(System.getenv("OSSRH_PASSWORD")))
        }
    }
}

signing {
    def key = decodeBase64(System.getenv("SIGNING_KEY"))
    def password = decodeBase64(System.getenv("SIGNING_PASSWORD"))

    useInMemoryPgpKeys(key, password)
    sign publishing.publications.mavenJava
}
